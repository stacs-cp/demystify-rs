language ESSENCE' 1.0
$ Futoshiki puzzle solver, originally modelled by Andras Salamon
$ see http://en.wikipedia.org/wiki/Futoshiki
$ Adapted by Joan Espasa Arxer to have parametric grid_size grid and reified constraints for the demistify tool

$ we are assuming always a square grid.
given grid_size: int(1..)

$ the numeric values specified in the puzzle
given     values : matrix indexed by [int(1..grid_size),int(1..grid_size)] of int(0..grid_size)

letting   VALUES be domain int( 0..grid_size )
letting   RANGE  be domain int( 1..grid_size )

$ index of last lt entry
given     lastdx : int( 1..((2*grid_size*(grid_size-1)-1)) )

letting   NUMQD  be domain int(0..(lastdx))


$ list of < relations in the problem
given     lt     : matrix indexed by [NUMQD, int(0..3)] of RANGE

$#VAR field
find      field  : matrix indexed by [RANGE, RANGE] of RANGE


$ -------- Reified variables -----------------------------

$#CON col_alldiff "cells ({{index[0]}},{{index[1]}}) and ({{index[0]}},{{index[2]}}) cannot both be {{index[3]}} as they are in the same column"
find col_alldiff: matrix indexed by [RANGE,RANGE,RANGE,RANGE] of bool

$#CON row_alldiff "cells ({{index[0]}},{{index[1]}}) and ({{index[0]}},{{index[2]}}) cannot both be {{index[3]}} as they are in the same row"
find row_alldiff: matrix indexed by [RANGE,RANGE,RANGE,RANGE] of bool

$#CON less_than "{% set i = index[0] + 1 %}cell ({{params['lt'][i][1]}},{{params['lt'][i][2]}}), must be less than cell ({{params['lt'][i][3]}},{{params['lt'][i][4]}})"
find less_than : matrix indexed by [NUMQD] of bool

such that

$ all columns have to be different
forAll i:RANGE.
    forAll j1,j2:RANGE. j1 < j2 ->
        forAll d:RANGE. col_alldiff[i,j1,j2,d] -> !(field[i,j1] = d /\ field[i,j2]=d),

$ all rows have to be different
forAll j:RANGE.
    forAll i1,i2:RANGE. i1 < i2 ->
        forAll d:RANGE. row_alldiff[j,i1,i2,d] -> !(field[i1,j] = d /\ field[i2,j]=d),

$ all < constraints are satisfied
    forAll i : NUMQD .
	less_than[i] -> ( field[ lt[i,0], lt[i,1] ] < field[ lt[i,2], lt[i,3] ] ),

$ set initial values
    forAll row,col : RANGE .
	( values[row,col] > 0) ->
	    (field[row,col] = values[row,col] ),

$ check if problem is sat
$and([and(row_alldiff), and(col_alldiff), and(less_than)]),

true
